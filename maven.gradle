
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'com.oguzbabaoglu'
version = '1.0.0'

def bin_user = ""
def bin_key = ""
def oss_user = ""
def oss_pass = ""
try {
    bin_user = BINTRAY_USER
    bin_key = BINTRAY_KEY
    oss_user = OSS_USER
    oss_pass = OSS_PASS
} catch (Exception ignored) {
    println 'Failed to find credentials. Maven deploy disabled'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId 'com.oguzbabaoglu'
            artifactId project.ext.pomId
            version = project.version
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'ButterFork'
                    description 'ButterKnife fork with Android library support.'
                    url 'https://github.com/oguzbabaoglu/butterfork'
                    scm {
                        url 'https://github.com/oguzbabaoglu/butterfork'
                        connection 'scm:git@https://github.com/oguzbabaoglu/butterfork.git'
                        developerConnection 'scm:git@https://github.com/oguzbabaoglu/butterfork.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'oguzbabaoglu'
                            name 'Oguz Babaoglu'
                            email 'oguz.babaoglu@gmail.com'
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = bin_user
    key = bin_key
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven'
        name = project.ext.pomId
        desc = 'ButterKnife fork with Android library support.'
        websiteUrl = 'https://github.com/oguzbabaoglu/butterfork'
        issueTrackerUrl = 'https://github.com/oguzbabaoglu/butterfork/issues'
        vcsUrl = 'https://github.com/oguzbabaoglu/butterfork.git'
        licenses = ['Apache-2.0']
        version {
            name = project.version
            vcsTag = project.version
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = oss_user
                password = oss_pass
            }
        }
    }
}
